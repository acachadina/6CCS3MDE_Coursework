grammar uk.ac.kcl.inf.DotLanguage with org.eclipse.xtext.common.Terminals

generate dotLanguage "http://www.ac.uk/kcl/inf/DotLanguage"


DotLanguage:
	graphs += (Graph | Digraph)*;
	
	
Graph:
	"graph" name=ID "{" statements += UndirectedStatement* "}"
;

Digraph:
	"digraph" name=ID "{" statements += DirectedStatement* "}"
;

DirectedStatement: 
	( 
		NodeDeclaration | 
		DirectedEdgeDeclaration
	)
	("," | ";")?
	
;

UndirectedStatement: 
	(
		NodeDeclaration | 
		UndirectedEdgeDeclaration 
	)
	("," | ";")?
;


NodeDeclaration:
	nodeName = NodeId (optionalAttributes = AttributeList)?
;

NodeId:
	name = ID
;

// the only supported attributes for now 
// will be 'color' and 'label'
// color defaults to black and the label to the node's ID
AttributeList:
	"[" attr += Attribute+ "]"
;

Attribute:
	attributeName = ID "=" attributeValue = IDORINT (',')?
;

IDORINT :
	ID | INT
;

// a node can be a reference to a declared node for now
DirectedEdgeDeclaration:
	firstNode = [NodeId] edge = DirectedEdge secondNode = RightEdgeDeclaration
;

UndirectedEdgeDeclaration:
	firstNode = [NodeId] edge = UndirectedEdge secondNode = RightEdgeDeclaration
;

RightEdgeDeclaration:
	secondNode = [NodeId] |
	nodeList = NodeList
;

NodeList returns NodeList:
	'{' nodes += [NodeId]+ '}'
;

DirectedEdge:
	"->"
;

UndirectedEdge:
	"--"
;